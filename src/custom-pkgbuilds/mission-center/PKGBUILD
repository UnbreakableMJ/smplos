# smplOS custom build of Mission Center
# Based on Arch official PKGBUILD by Peter Jung <ptr1337@archlinux.org>
# Adds: flat process list view, wrapper-script app detection fix

pkgname=mission-center
pkgver=1.1.0.r1
pkgrel=1
pkgdesc="Monitor your CPU, Memory, Disk, Network and GPU usage (smplOS patched)"
arch=(x86_64)
url="https://missioncenter.io"
license=(GPL-3.0-or-later)
depends=(
  dconf
  dmidecode
  gcc-libs
  gdk-pixbuf2
  glib2
  glibc
  graphene
  gtk4
  libadwaita
  mesa
  nvtop
  protobuf
  systemd
  systemd-libs
)
makedepends=(
  blueprint-compiler
  cmake
  git
  meson
  pkgconf
  protobuf
)
# Build from our forks with flat-process-list + app-detection patches
# All three repos need matching branches because submodule pointers reference our custom commits
source=("git+https://github.com/KonTy/mission-center.git#branch=flat-process-list-and-app-detection"
        "git+https://gitlab.com/kdedev/gng.git#branch=flat-process-list-and-app-detection"
        "git+https://gitlab.com/kdedev/app-detection.git#branch=flat-process-list-and-app-detection")
sha256sums=('SKIP'
            'SKIP'
            'SKIP')
provides=("mission-center=$pkgver")
conflicts=(mission-center)

prepare() {
  cd "$pkgname"

  git submodule init
  git config submodule.subprojects/magpie.url "${srcdir}/gng"
  git -c protocol.file.allow=always submodule update

  # Wire up magpie's nested submodule (app-rummage)
  cd subprojects/magpie
  git submodule init
  git config submodule.platform-linux/crates/app-rummage.url "${srcdir}/app-detection"
  git -c protocol.file.allow=always submodule update
  cd "$srcdir/$pkgname"

  CARGO_HOME="$srcdir/build/cargo-home" \
    cargo fetch --target "$(rustc --print host-tuple)"

  cd subprojects/magpie
  CARGO_HOME="$srcdir/build/subprojects/magpie/cargo-home" \
    cargo fetch --target "$(rustc --print host-tuple)"
}

build() {
  CFLAGS+=" -ffat-lto-objects"
  arch-meson "$pkgname" build --buildtype=release

  CARGO_PROFILE_RELEASE_LTO=true \
    CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1 \
    CARGO_PROFILE_RELEASE_DEBUG=2 \
    CARGO_PROFILE_RELEASE_STRIP=false \
    meson compile -C build
}

package() {
  meson install -C build --no-rebuild --destdir "$pkgdir"
}
